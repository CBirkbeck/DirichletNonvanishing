import Mathlib.NumberTheory.LSeries.DirichletContinuation
import Mathlib.NumberTheory.LSeries.Dirichlet
import Project.EulerProducts.Auxiliary

/-!
# Non-vanishing of `L(Ï‡, 1)` for nontrivial quadratic characters `Ï‡`
-/

open Complex

/-- The object we're trying to show doesn't exist. -/
structure BadChar (N : â„•) [NeZero N] where
  Ï‡ : DirichletCharacter â„‚ N
  Ï‡_ne : Ï‡ â‰  1
  Ï‡_sq : Ï‡ ^ 2 = 1
  hÏ‡ : Ï‡.LFunction 1 = 0

variable {N : â„•} [NeZero N]

noncomputable section

/-- The associated character is quadratic. -/
lemma BadChar.Ï‡_apply_eq (B : BadChar N) (x : ZMod N) :
    B.Ï‡ x = 0 âˆ¨ B.Ï‡ x = 1 âˆ¨ B.Ï‡ x = -1 := by
  by_cases hx : IsUnit x
  Â· have hx' : (B.Ï‡ x) ^ 2 = 1 := by
      rw [â† B.Ï‡.pow_apply' two_ne_zero, B.Ï‡_sq, MulChar.one_apply hx]
    rw [sq_eq_one_iff] at hx'
    tauto
  Â· simp only [B.Ï‡.map_nonunit hx, true_or]

/-- The auxiliary function `F: s â†¦ Î¶ s * L B.Ï‡ s`. -/
def BadChar.F (B : BadChar N) : â„‚ â†’ â„‚ :=
  Function.update (fun s : â„‚ â†¦ riemannZeta s * B.Ï‡.LFunction s) 1 (deriv B.Ï‡.LFunction 1)

lemma BadChar.F_differentiableAt_of_ne (B : BadChar N) {s : â„‚} (hs : s â‰  1) :
    DifferentiableAt â„‚ B.F s := by
  apply DifferentiableAt.congr_of_eventuallyEq
  Â· exact (differentiableAt_riemannZeta hs).mul <| B.Ï‡.differentiableAt_LFunction s (.inl hs)
  Â· filter_upwards [eventually_ne_nhds hs] with t ht using Function.update_noteq ht ..

lemma BadChar.F_differentiable (B : BadChar N) : Differentiable â„‚ B.F := by
  intro s
  rcases ne_or_eq s 1 with hs | rfl
  Â· exact B.F_differentiableAt_of_ne hs
  Â· apply AnalyticAt.differentiableAt
    apply analyticAt_of_differentiable_on_punctured_nhds_of_continuousAt
    Â· filter_upwards [self_mem_nhdsWithin] with t ht
      exact B.F_differentiableAt_of_ne ht
    -- now reduced to showing *continuity* at s = 1
    let G := Function.update (fun s â†¦ (s - 1) * riemannZeta s) 1 1
    let H := Function.update (fun s â†¦ B.Ï‡.LFunction s / (s - 1)) 1 (deriv B.Ï‡.LFunction 1)
    have : B.F = G * H := by
      ext t
      rcases eq_or_ne t 1 with rfl | ht
      Â· simp only [F, G, H, Pi.mul_apply, one_mul, Function.update_same]
      Â· simp only [F, G, H, Pi.mul_apply, Function.update_noteq ht]
        field_simp [sub_ne_zero.mpr ht]
        ring
    rw [this]
    apply ContinuousAt.mul
    Â· simpa only [G, continuousAt_update_same] using riemannZeta_residue_one
    Â· have : HasDerivAt B.Ï‡.LFunction (deriv B.Ï‡.LFunction 1) 1 :=
        (B.Ï‡.differentiableAt_LFunction 1 (.inr B.Ï‡_ne)).hasDerivAt
      rw [hasDerivAt_iff_tendsto_slope] at this
      simp only [funext (slope_def_field B.Ï‡.LFunction 1), B.hÏ‡, sub_zero] at this
      rw [Metric.continuousAt_iff']
      intro Îµ hÎµ
      simp only [Metric.tendsto_nhds, eventually_nhdsWithin_iff] at this
      filter_upwards [this Îµ hÎµ] with a ha
      rcases eq_or_ne a 1 with rfl | ha'
      Â· simp only [dist_self, hÎµ]
      Â· simpa only [H, Function.update_noteq ha', Function.update_same] using ha ha'

/-- The trivial zero at `s = -2` of the zeta function gives that `F (-2) = 0`.
This is used later to obtain a contradction. -/
lemma BadChar.F_neg_two (B : BadChar N) : B.F (-2) = 0 := by
  simp only [BadChar.F]
  have := riemannZeta_neg_two_mul_nat_add_one 0
  rw [Nat.cast_zero, zero_add, mul_one] at this
  rw [Function.update_noteq (mod_cast (by omega : (-2 : â„¤) â‰  1)), this, zero_mul]

/-- The complex-valued arithmetic function whose L-series is `B.F`. -/
def BadChar.e (B : BadChar N) : ArithmeticFunction â„‚ := .zeta * toArithmeticFunction (B.Ï‡ Â·)

lemma BadChar.F_eq_LSeries (B : BadChar N) {s : â„‚} (hs : 1 < s.re) :
    B.F s = LSeries (B.e Â·) s := by
  have (n : â„•) : B.e n = LSeries.convolution (fun _ â†¦ 1) (B.Ï‡ Â·) n := by
    simp only [e, ArithmeticFunction.mul_apply, ArithmeticFunction.natCoe_apply,
      ArithmeticFunction.zeta_apply, Nat.cast_ite, Nat.cast_zero, Nat.cast_one, ite_mul, zero_mul,
      one_mul, LSeries.convolution_def]
    refine Finset.sum_congr rfl fun i hi â†¦ ?_
    simp only [(Nat.ne_zero_of_mem_divisorsAntidiagonal hi).1, â†“reduceIte, toArithmeticFunction,
      ArithmeticFunction.coe_mk, (Nat.ne_zero_of_mem_divisorsAntidiagonal hi).2]
  simp only [this]
  rw [LSeries_convolution', BadChar.F, Function.update_noteq]
  Â· congr 1
    Â· exact (LSeriesHasSum_one hs).LSeries_eq.symm
    Â· exact DirichletCharacter.LFunction_eq_LSeries _ hs
  Â· intro h
    simp only [h, one_re, lt_self_iff_false] at hs
  Â· rwa [â† Pi.one_def, LSeriesSummable_one_iff]
  Â· exact ZMod.LSeriesSummable_of_one_lt_re _ hs

lemma BadChar.mult_e (B : BadChar N) : B.e.IsMultiplicative := by
  apply ArithmeticFunction.isMultiplicative_zeta.natCast.mul
  refine ArithmeticFunction.IsMultiplicative.iff_ne_zero.mpr âŸ¨?_, ?_âŸ©
  Â· simp only [toArithmeticFunction, ArithmeticFunction.coe_mk, one_ne_zero, â†“reduceIte,
      Nat.cast_one, map_one]
  Â· intro m n hm hn _
    simp only [toArithmeticFunction, ArithmeticFunction.coe_mk, mul_eq_zero, hm, hn, false_or,
      if_false, Nat.cast_mul, map_mul]

-- We use the ordering on `â„‚` given by comparing real parts for fixed imaginary part
open scoped ComplexOrder

lemma BadChar.e_prime_pow (B : BadChar N) {p : â„•} (hp : p.Prime) (k : â„•) :
    0 â‰¤ B.e (p ^ k) := by
  simp only [e, toArithmeticFunction, ArithmeticFunction.coe_zeta_mul_apply,
    ArithmeticFunction.coe_mk, Nat.sum_divisors_prime_pow hp, pow_eq_zero_iff', hp.ne_zero, ne_eq,
    false_and, â†“reduceIte, Nat.cast_pow, map_pow]
  rcases B.Ï‡_apply_eq p with h | h | h
  Â· refine Finset.sum_nonneg fun i _ â†¦ ?_
    simp [h, le_refl, pow_nonneg]
  Â· refine Finset.sum_nonneg fun i _ â†¦ ?_
    simp only [h, one_pow, zero_le_one]
  Â· simp only [h, neg_one_geom_sum]
    split_ifs
    exacts [le_rfl, zero_le_one]

lemma BadChar.e_nonneg (B : BadChar N) (n : â„•) : 0 â‰¤ B.e n := by
  rcases eq_or_ne n 0 with rfl | hn
  Â· simp only [ArithmeticFunction.map_zero, le_refl]
  Â· simpa only [B.mult_e.multiplicative_factorization _ hn] using
      Finset.prod_nonneg fun p hp â†¦ B.e_prime_pow (Nat.prime_of_mem_primeFactors hp) _

lemma BadChar.e_one_eq_one (B : BadChar N) : B.e 1 = 1 := by
  simp only [e, toArithmeticFunction, ArithmeticFunction.mul_apply, Nat.divisorsAntidiagonal_one,
    Prod.mk_one_one, ArithmeticFunction.natCoe_apply, ArithmeticFunction.zeta_apply, Nat.cast_ite,
    Nat.cast_zero, Nat.cast_one, ArithmeticFunction.coe_mk, mul_ite, mul_zero, ite_mul, zero_mul,
    one_mul, Finset.sum_singleton, Prod.snd_one, one_ne_zero, â†“reduceIte, Prod.fst_one, map_one]

open ArithmeticFunction in
open scoped LSeries.notation in
lemma BadChar.e_summable (B : BadChar N) {s : â„‚} (hs : 1 < s.re) : LSeriesSummable (B.e Â·) s := by
  have : LSeriesSummable (toArithmeticFunction (â†—B.Ï‡)) s := by
    refine (LSeriesSummable_congr s fun {n} hn â†¦ ?_).mp <| B.Ï‡.LSeriesSummable_of_one_lt_re hs
    simp only [toArithmeticFunction, coe_mk, hn, â†“reduceIte]
  exact ArithmeticFunction.LSeriesSummable_mul (LSeriesSummable_zeta_iff.mpr hs) this

/-- If all values of a `â„‚`-valued arithmetic function are nonnegative reals and `x` is a
real number in the domain of absolute convergence, then the `n`th iterated derivative
of the associated L-series is nonnegative real when `n` is even and nonpositive real
when `n` is odd. -/
lemma derivs_from_coeffs (a : ArithmeticFunction â„‚) (hn : âˆ€ n, 0 â‰¤ a n) (x : â„)
    (h : LSeries.abscissaOfAbsConv (a Â·) < x) (n : â„•) :
    0 â‰¤ (-1) ^ n * iteratedDeriv n (LSeries (a Â·)) x := by
  rw [LSeries_iteratedDeriv _ h]
  Â· rw [LSeries]
    ring_nf
    simp only [even_two, Even.mul_left, Even.neg_pow, one_pow, mul_one]
    apply tsum_nonneg
    intro k
    rw [LSeries.term_def]
    rcases eq_or_ne k 0 with rfl | hm
    Â· simp only [â†“reduceIte, le_refl]
    Â· simp only [hm, â†“reduceIte]
      apply mul_nonneg
      Â· induction' n with n IH
        Â· simp only [Function.iterate_zero, id_eq]
          Â· exact hn k
        Â· rw [add_comm, Function.iterate_add_apply, Function.iterate_one]
          apply mul_nonneg _ (IH)
          simp only [â† natCast_log, zero_le_real, Real.log_natCast_nonneg]
      Â· simp only [le_def, zero_re, inv_re, zero_im, inv_im]
        constructor
        Â· apply div_nonneg _ (normSq_nonneg _)
          simp only [cpow_def, Nat.cast_eq_zero, hm, â†“reduceIte, â† natCast_log, â† ofReal_mul,
            exp_ofReal_re, Real.exp_nonneg]
        Â· simp only [cpow_def, Nat.cast_eq_zero, hm, â†“reduceIte, â† natCast_log, â† ofReal_mul,
          exp_ofReal_im, neg_zero, zero_div]

lemma BadChar.F_two_pos (B : BadChar N) : 0 < B.F 2 := by
  rw [B.F_eq_LSeries (by norm_num), LSeries]
  refine tsum_pos (B.e_summable (by norm_num)) (fun n â†¦ ?_) 1 ?_ ; swap
  Â· simp only [LSeries.term_def, one_ne_zero, â†“reduceIte, e_one_eq_one, Nat.cast_one, cpow_ofNat,
      one_pow, ne_eq, not_false_eq_true, div_self, zero_lt_one]
  Â· simp only [LSeries.term_def, cpow_ofNat]
    split
    Â· simp only [le_refl]
    Â· exact mul_nonneg (B.e_nonneg n) <|
        (RCLike.inv_pos_of_pos (show 0 < ((n : â„‚) ^ 2) by positivity)).le

section iteratedDeriv

variable {ğ•œ F} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup F] [NormedSpace ğ•œ F]

-- the lemmas in this section should go to Mathlib.Analysis.Calculus.Deriv.Shift
lemma iteratedDeriv_comp_const_add (n : â„•) (f : ğ•œ â†’ F) (s : ğ•œ) :
    iteratedDeriv n (fun z â†¦ f (s + z)) = fun t â†¦ iteratedDeriv n f (s + t) := by
  induction n with
  | zero => simp only [iteratedDeriv_zero]
  | succ n IH =>
      simp only [iteratedDeriv_succ, IH]
      ext1 z
      exact deriv_comp_const_add (iteratedDeriv n f) s z

lemma iteratedDeriv_comp_add_const (n : â„•) (f : ğ•œ â†’ F) (s : ğ•œ) :
    iteratedDeriv n (fun z â†¦ f (z + s)) = fun t â†¦ iteratedDeriv n f (t + s) := by
  induction n with
  | zero => simp only [iteratedDeriv_zero]
  | succ n IH =>
      simp only [iteratedDeriv_succ, IH]
      ext1 z
      exact deriv_comp_add_const (iteratedDeriv n f) s z

lemma iteratedDeriv_eq_on_open (n : â„•) {f g : ğ•œ â†’ F} {s : Set ğ•œ} (hs : IsOpen s) (x : s)
    (hfg : Set.EqOn f g s) : iteratedDeriv n f x = iteratedDeriv n g x := by
  induction' n with n IH generalizing f g
  Â· simpa only [iteratedDeriv_zero] using hfg x.2
  Â· simp only [iteratedDeriv_succ']
    exact IH fun y hy â†¦ Filter.EventuallyEq.deriv_eq <|
      Filter.eventuallyEq_iff_exists_mem.mpr âŸ¨s, IsOpen.mem_nhds hs hy, hfgâŸ©

end iteratedDeriv

theorem BadChar.abscissa {N : â„•} [NeZero N] (B : BadChar N) :
    LSeries.abscissaOfAbsConv B.e < (2 : â„) := by
  suffices LSeries.abscissaOfAbsConv B.e â‰¤ (3 / 2 : â„) from this.trans_lt <| by norm_cast; norm_num
  convert LSeriesSummable.abscissaOfAbsConv_le (s := (3 / 2 : â„)) ?_
  exact B.e_summable (s := (3 / 2 : â„))
    (by simp only [ofReal_div, ofReal_ofNat, div_ofNat_re, re_ofNat]; norm_num)

/-- The goal: bad characters do not exist. -/
theorem BadChar.elim (B : BadChar N) : False := by
  have h1 := B.e_nonneg
  have h2 := B.F_neg_two
  have h3 := B.F_differentiable
  have h5 := derivs_from_coeffs B.e (fun n â†¦ h1 n) 2 (BadChar.abscissa B)
  Â· have := Complex.at_zero_le_of_iteratedDeriv_alternating (f := fun s â†¦ B.F (s + 2)) (z := -4)
      ?_ ?_
    Â· simp only [Left.neg_nonpos_iff, Nat.ofNat_nonneg, zero_add, true_implies] at this
      have h6 := BadChar.F_two_pos B
      have : 0 < B.F (-2) := by
        apply lt_of_lt_of_le h6
        norm_cast at *
      linarith
    Â· apply Differentiable.comp h3
      simp only [differentiable_id', differentiable_const, Differentiable.add]
    Â· intro n _
      have h55 := h5 n
      rw [iteratedDeriv_comp_add_const n B.F 2]
      have := iteratedDeriv_eq_on_open n (f := fun s â†¦ B.F (s + 2))
        (g := fun z â†¦ LSeries (fun x â†¦ â†‘(B.e x)) (â†‘2 + z)) (s := {s | 1 < (s + 2).re}) ?_ ?_
          (x := âŸ¨(0 : â„‚), by simp only [add_re, re_ofNat, Set.mem_setOf_eq, zero_re, zero_add,
            Nat.one_lt_ofNat]âŸ©)
      Â· rw [this]
        apply h55
      Â· apply isOpen_lt
        exact continuous_const
        refine Continuous.comp' (Complex.continuous_re) (continuous_add_right 2)
      Â· intro x hx
        simp only
        rw [B.F_eq_LSeries (s := x + 2), add_comm]
        apply hx
